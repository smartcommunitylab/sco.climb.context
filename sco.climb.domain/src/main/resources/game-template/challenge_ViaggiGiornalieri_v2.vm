#[[
package eu.trentorise.game.model

import eu.trentorise.game.core.Utility;

global Utility utils;
global String badge_collection_name;
global String const_school_name;

rule 'ch-ViaggiGiornalieriZeroImpact-check'
salience -1000
lock-on-active
when
   $challenge : ChallengeConcept( 
   					modelName == "ViaggiGiornalieri", 
   					isCompleted() != true,
					fields["prizeWon"] == false,
					$target: fields["_target_numTrips_"], 
					$vp : fields["VirtualPrize"]) 
	Player($teamId : id, team == true, $teamId == const_school_name) // only applies to SCHOOL
	$walkTrips : PointConcept(name == "walk_trips" )
	$bikeTrips : PointConcept(name == "bike_trips" )
	$pedibus : PointConcept(name == "pedibus_trips" )
	Game( $gameId: id) 
then
	utils.log("apply \'ch-ViaggiGiornalieri-check\'");
	double zeroImpactTrips = $walkTrips.getPeriodCurrentScore("daily") 
		+ $bikeTrips.getPeriodCurrentScore("daily")
		+ $pedibus.getPeriodCurrentScore("daily");
	if (zeroImpactTrips >= (double) $target) {
		utils.log("Challenge ViaggiGiornalieri HAS BEEN WON by: " + $teamId);
		MessageNotification mn = new MessageNotification($gameId,$teamId,"ChallengeWon");
		mn.addData("_VirtualPrize_", $vp);
		insert(mn);
		$challenge.getFields().put("prizeWon", (Boolean) true);
		update($challenge); 
	}
end

rule 'ch-ViaggiGiornalieriZeroImpact-award'
salience -5000
when
	$challenge : ChallengeConcept( 
					modelName == "ViaggiGiornalieri", 
					isCompleted() != true,
					fields["prizeWon"] == true,
					$leg : fields["legName"], 
					$bonusPointType : fields["bonusPointType"], 
					$bonusScore : fields["bonusScore"] )
	Player($teamId : id, team == true)
    BadgeCollectionConcept( 
    	name == badge_collection_name, 
    	badgeEarned contains (String) $leg || $leg == null )
	$pc : PointConcept(name == (String) $bonusPointType)
	$tot_dist : PointConcept(name == "total_distance")
then
	utils.log("apply \'ch-ViaggiGiornalieri-award\'");
    $pc.setScore($pc.getScore() +  (Double) $bonusScore);
	update($pc);
	if (! ((String) $bonusPointType).equals("total_distance")) {
		$tot_dist.setScore($tot_dist.getScore() +  (Double) $bonusScore);
		update($tot_dist);
	}
	$challenge.completed();
    update($challenge);
end
]]#